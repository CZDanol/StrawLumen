; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppVersion "1.3.0.0"
#define MyAppPublisher "Straw Solutions"
#define MyAppCopyright "(c) 2018 Straw Solutions, Daniel Èejchan"
#define MyAppURL "http://straw-solutions.cz"
#define MyAppExeName "strawLumen.exe"

[Setup]
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} v{#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
VersionInfoVersion={#MyAppVersion}
VersionInfoCompany={#MyAppPublisher}
VersionInfoCopyRight={#MyAppCopyright}
DisableProgramGroupPage=yes
OutputDir=setup
OutputBaseFilename=lumen_{#PlatformId}
Compression=lzma
SolidCompression=yes
UninstallDisplayIcon="{app}\bin_{#PlatformId}\{#MyAppExeName}"
; Sign tools configured in inno setup ide
SignTool=ComodoSign
SignedUninstaller=yes
DisableDirPage=no

[Languages]
Name: "czech"; MessagesFile: "compiler:Languages\Czech.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Files]
Source: "installDeps\vc_redist.{#PlatformId}.exe"; DestDir: {tmp}; AfterInstall: InstallMSVC; Flags: deleteafterinstall
Source: "installDeps\klcp_basic_unattended.ini"; DestDir: {tmp}; Flags: deleteafterinstall
Source: "installDeps\k_lite.exe"; DestDir: {tmp}; AfterInstall: InstallKLite; Flags: deleteafterinstall

Source: "bin\bin_{#PlatformId}\strawLumen.exe"; DestDir: "{app}\bin_{#PlatformId}"; Flags: ignoreversion sign
Source: "bin\bin_{#PlatformId}\QtWebEngineProcess.exe"; DestDir: "{app}\bin_{#PlatformId}"; Flags: ignoreversion recursesubdirs
Source: "bin\bin_{#PlatformId}\*.dll"; DestDir: "{app}\bin_{#PlatformId}"; Flags: ignoreversion recursesubdirs
Source: "bin\bin_{#PlatformId}\*.pak"; DestDir: "{app}\bin_{#PlatformId}"; Flags: ignoreversion recursesubdirs
Source: "bin\bin_{#PlatformId}\*.dat"; DestDir: "{app}\bin_{#PlatformId}"; Flags: ignoreversion recursesubdirs

Source: "bin\backgrounds\*"; DestDir: "{app}\backgrounds"; Flags: ignoreversion
Source: "bin\bible\*"; DestDir: "{app}\bible"; Flags: ignoreversion
Source: "bin\etc\*"; DestDir: "{app}\etc"; Flags: ignoreversion recursesubdirs
Source: "bin\changelog.html"; DestDir: "{app}\changelog.html"; Flags: ignoreversion recursesubdirs
Source: "fonts\SourceSansPro-*.ttf"; DestDir: "{fonts}"; FontInstall: "Source Sans Pro"; Flags: onlyifdoesntexist uninsneveruninstall
Source: "portableMode"; DestDir: "{app}"; Components: portable
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Types]
Name: "full"; Description: "Nepøenosná instalace"
Name: "portable"; Description: "Portable/pøenosná instalace"

[Components]
Name: "core"; Description: "Aplikace"; Types: full portable; Flags: fixed
Name: "portable"; Description: "Portable/pøenosný mód (data programu se ukládají do složky s aplikací)"; Types: portable

[Dirs]

[Icons]
Name: "{app}\{#MyAppName}"; Filename: "{app}\bin_{#PlatformId}\{#MyAppExeName}"
Name: "{group}\{#MyAppName}"; Filename: "{app}\bin_{#PlatformId}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\bin_{#PlatformId}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\bin_{#PlatformId}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
procedure InstallMSVC;
var
  StatusText: string;
  ResultCode: integer;
begin
  StatusText := WizardForm.StatusLabel.Caption;
  WizardForm.StatusLabel.Caption := 'Instaluji Microsoft Visual Studio Redistributable...';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    if not Exec(ExpandConstant('{tmp}\vc_redist.{#PlatformId}.exe'), '/install /passive /quiet /norestart', '', SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode)
    then
      MsgBox('Nepodaøilo se spustit instalátor MSVC Redist. Aplikace bez této knihovny nemusí fungovat.' + #13#10 + SysErrorMessage(ResultCode), mbError, MB_OK);
  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;
  end;
end;

procedure InstallKLite;
var
  StatusText: string;
  ResultCode: integer;
begin
  StatusText := WizardForm.StatusLabel.Caption;
  WizardForm.StatusLabel.Caption := 'Instaluji K-Lite codec pack...';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    if not Exec(ExpandConstant('{tmp}\k_lite.exe'), ExpandConstant('/verysilent /norestart /LoadInf="{tmp}/klcp_basic_unattended.ini"'), '', SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode)
    then
      MsgBox('Nepodaøilo se spustit instalátor K-Lite. Bez tìchto kodekù nemusí fungovat funkce pøehrávaèe videí.' + #13#10 + SysErrorMessage(ResultCode), mbError, MB_OK);
  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;
  end;
end;

